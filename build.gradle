plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.2.2'
}

group = 'uk.co.hawks_haven'
version = '0.0.2'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven { url = "https://s01.oss.sonatype.org/content/repositories/snapshots" }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = "https://repo.codemc.org/repository/maven-public/" }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
//    implementation "dev.jorel:commandapi-bukkit-shade:9.3.0"
//    implementation "dev.jorel:commandapi-bukkit-shade:9.4.0-SNAPSHOT"
    implementation "dev.jorel:commandapi-bukkit-shade:9.4.0-20240226.110159-17"
}

shadowJar {
    dependencies {
//        include dependency("dev.jorel:commandapi-bukkit-shade:9.3.0")
//        include dependency("dev.jorel:commandapi-bukkit-shade:9.4.0-SNAPSHOT")
        include dependency("dev.jorel:commandapi-bukkit-shade:9.4.0-20240226.110159-17")
    }
    relocate("dev.jorel.commandapi", "uk.co.hawks_haven.commandapitest.shaded.dev.jorel.commandapi")
}

tasks {
    runServer {
        minecraftVersion("1.20.4")
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
